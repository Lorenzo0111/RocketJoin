/*
 *  This file is part of RocketJoin, licensed under the MIT License.
 *
 *  Copyright (c) Lorenzo0111
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

//java.sourceCompatibility = JavaVersion.VERSION_1_8

ext {
    deps = "me.lorenzo0111.rocketjoin.libraries."
}

version = '2.4.2'

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    group = 'me.lorenzo0111'
    version = rootProject.version
    description = "Create custom join messages with groups, commands on join, sounds and more. PlaceholderAPI support"

    repositories {
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://nexus.velocitypowered.com/repository/maven-public/' }
        maven { url 'https://repo.spongepowered.org/repository/maven-public/' }
        maven { url 'https://dl.lorenzo0111.me/releases' }
    }

    dependencies {
        compileOnly('org.jetbrains:annotations:24.1.0')
        implementation('com.google.code.gson:gson:2.11.0')
        implementation('org.spongepowered:configurate-yaml:4.1.2')
        implementation('me.lorenzo0111.pluginslib:common:2.0.4') {
            exclude module: 'slimjar'
        }
    }

    jar {
        archiveFileName = "RocketJoin-${project.name}-${project.version}.jar"
    }

    shadowJar {
        relocate("net.kyori.examination", "${deps}examination")
        relocate("org.checkerframework", "${deps}checkerframework")
        relocate("io.github.slimjar", "${deps}slimjar")
        relocate("org.yaml", "${deps}yaml")
        relocate("io.leangen", "${deps}leangen")
        relocate("com.google.thirdparty", "${deps}google.thirdparty")
        relocate("com.google.j2objc", "${deps}google.j2objc")
        relocate("com.google.errorprone", "${deps}google.errorprone")
        relocate("com.google.common", "${deps}google.common")
        relocate("javax", "${deps}javax")
        relocate("org.bstats", "${deps}bstats") {
            exclude('org.bstats.sponge')
        }

        // Remove META-INF
        exclude 'META-INF', 'META-INF/**'

        archiveFileName = "RocketJoin-${project.name}-${project.version}.jar"
    }

    processResources {
        from(sourceSets.main.resources.srcDirs) {
            duplicatesStrategy DuplicatesStrategy.INCLUDE
            inputs.property 'version', version
            filter ReplaceTokens, tokens: [version: version]
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    tasks.build.dependsOn tasks.shadowJar
}