/*
 *  This file is part of RocketJoin, licensed under the MIT License.
 *
 *  Copyright (c) Lorenzo0111
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

java.sourceCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    group = 'me.lorenzo0111'
    version = '2.1.1'
    description = "Create custom join messages with groups, commands on join, sounds and more. PlaceholderAPI support"

    repositories {
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://repo.repsy.io/mvn/lorenzo0111/public' }
        maven { url 'https://nexus.velocitypowered.com/repository/maven-public/' }
        maven { url 'https://repo.spongepowered.org/repository/maven-public/' }
        maven { url 'https://repo.repsy.io/mvn/lorenzo0111/public/' }
        maven { url 'https://repo.vshnv.tech/releases/' }
    }

    dependencies {
        compileOnly('org.jetbrains:annotations:22.0.0')
        implementation('com.google.code.gson:gson:2.8.8')
        implementation('org.spongepowered:configurate-yaml:4.1.2')
    }

    jar {
        archiveFileName = "RocketJoin-${project.name}-${project.version}.jar"
    }

    shadowJar {
        relocate('me.lorenzo0111.pluginslib', 'me.lorenzo0111.rocketjoin.pluginslib')
        relocate('net.kyori.examination', 'me.lorenzo0111.rocketjoin.libs.examination')
        relocate('org.checkerframework', 'me.lorenzo0111.rocketjoin.libs.checkerframework')
        relocate('io.github.slimjar', 'me.lorenzo0111.rocketjoin.libs.slimjar')
        relocate('org.yaml', 'me.lorenzo0111.rocketjoin.libs.yaml')
        relocate('io.leangen', 'me.lorenzo0111.rocketjoin.libs.leangen')
        relocate('com.google', 'me.lorenzo0111.rocketjoin.libs.google')
        relocate('org.bstats','me.lorenzo0111.rocketjoin.bstats') {
            exclude('org.bstats.sponge')
        }

        // Remove META-INF
        exclude 'META-INF', 'META-INF/**'

        archiveFileName = "RocketJoin-${project.name}-${project.version}.jar"
    }

    processResources {
        from(sourceSets.main.resources.srcDirs) {
            duplicatesStrategy DuplicatesStrategy.INCLUDE
            inputs.property 'version', version
            filter ReplaceTokens, tokens: [version: version]
        }
    }

    tasks.build.dependsOn tasks.shadowJar
}